group 'com.github.molcikas'
version '0.7.1'

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'findbugs'
    
    version = parent.version
    group = parent.group

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://clojars.org/repo" }
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.11'
        compile group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'

        // Logging
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
        compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.12'
        compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.12'
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.12'

        testCompile group: 'junit', name: 'junit', version: '4.11'

        findbugsPlugins group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: '1.7.1'
    }

    tasks.idea {
        dependsOn = [ cleanIdeaModule, ideaModule ]
    }

    idea {
        module {
            iml {
                beforeMerged { module ->
                    module.dependencies.clear()
                }
            }
        }
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    findbugs {
        toolVersion = "3.0.1"
        sourceSets = [sourceSets.main] //don't run against tests
        ignoreFailures = true //checkFindbugsReport task will handle the failure
        includeFilter = file(project.rootDir.absolutePath + "/findbugs-include-filter.xml")
        excludeFilter = file(project.rootDir.absolutePath + "/findbugs-exclude-filter.xml")
    }

    task checkFindBugsReport {
        doLast {
            def xmlReport = findbugsMain.reports.xml
            if (xmlReport.destination.exists()) {
                def slurped = new XmlSlurper().parse(xmlReport.destination)
                def bugsFound = slurped.BugInstance.size()

                slurped.BugInstance.each {
                    System.err <<
                            "Findbugs violation ${it.@category} : ${it.@type} in ${it.SourceLine.@classname}: ${it.SourceLine.@start}\n"
                }

                if (bugsFound > 0) {
                    throw new GradleException(
                            "$bugsFound FindBugs rule violations were found. See the report at: $xmlReport.destination")
                }
            }
        }
    }

    findbugsMain.finalizedBy checkFindBugsReport

    task analyze {
        dependsOn = [findbugsMain]
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

// Comment this in when building for a sonatype release.
// See http://central.sonatype.org/pages/gradle.html and https://oss.sonatype.org/
//    signing {
//        sign configurations.archives
//    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description project.description
                    url 'https://github.com/molcikas/photon'

                    scm {
                        connection 'https://github.com/molcikas/photon.git'
                        developerConnection 'https://github.com/molcikas/photon.git'
                        url 'https://github.com/molcikas/photon'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/molcikas/photon/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id developerId
                            name developerName
                            email developerEmailAddress
                        }
                    }
                }
            }
        }
    }
}