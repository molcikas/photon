group 'com.github.molcikas'
version '0.2.0'

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'
    
    version = parent.version
    group = parent.group

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.11'

        // Logging
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
        compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.12'
        compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.12'
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.12'

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    tasks.idea {
        dependsOn = [ cleanIdeaModule, ideaModule ]
    }

    idea {
        module {
            iml {
                beforeMerged { module ->
                    module.dependencies.clear()
                }
            }
        }
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description project.description
                    url 'https://github.com/molcikas/photon'

                    scm {
                        connection 'https://github.com/molcikas/photon.git'
                        developerConnection 'https://github.com/molcikas/photon.git'
                        url 'https://github.com/molcikas/photon'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/molcikas/photon/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'molcikas'
                            name 'Matt Olcikas'
                            email developerEmailAddress
                        }
                    }
                }
            }
        }
    }
}